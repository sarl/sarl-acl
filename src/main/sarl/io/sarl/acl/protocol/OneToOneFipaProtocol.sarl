/* 
 * $Id$
 * 
 * SARL is an general-purpose agent programming language.
 * More details on http://www.sarl.io
 * 
 * Copyright (C) 2014-2021 the original authors or authors.
 * 
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * 
 * http://www.apache.org/licenses/LICENSE-2.0
 * 
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

package io.sarl.acl.protocol

import java.util.Collection
import java.util.Collections
import java.util.UUID

/** 
 * Abstract implementation of a protocol with a single participant.
 * 
 * @author $Author: sgalland$
 * @version $FullVersion$
 * @mavengroupid $Groupid$
 * @mavenartifactid $ArtifactId$
 */
abstract behavior OneToOneFipaProtocol extends FipaProtocol {

	var participant : UUID

	final def getMaximumParticipants : int {
		2
	}

	final def getParticipants : Collection<UUID> {
		if (this.participant === null) {
			return Collections::emptyList
		}
		return Collections::singleton(this.participant)
	}

	/** Replies the single participant. */
	final def getParticipant : UUID {
		this.participant
	}

	final def isParticipantSide : boolean {
		this.participant !== null && this.participant == owner.ID
	}

	/** Initiate the protocol as an initiator.
	 *
	 * @param participant the identifier of the protocol participant.
	 */
	protected final def initiateAsInitiator(participant : UUID) {
		if (this.initiator !== null) {
			addTooManyInitiatorError
		} else if (this.participant !== null) {
			addTooManyParticipantError
		} else {
			this.initiator = owner.ID
			this.participant = participant
			this.conversationID = Utils::generateConversationID
		}
	}

	/** Initiate the protocol as a participant.
	 */
	protected final def initiateAsParticipant {
		if (this.participant !== null) {
			addTooManyParticipantError
		} else if (this.initiator == owner.ID) {
			addParticipantIsAlreadyInitiatorError
		} else {	
			this.participant = owner.ID
		}
	}
		
}
